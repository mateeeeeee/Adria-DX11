#include "ParticleUtil.hlsli"
#include <Common.hlsli>

Texture2D								RandomTexture		: register(t0);
RWStructuredBuffer<GPUParticlePartA>	ParticleBufferA		: register(u0);
RWStructuredBuffer<GPUParticlePartB>	ParticleBufferB		: register(u1);
ConsumeStructuredBuffer<uint>			DeadListToAllocFrom	: register(u2);

[numthreads(1024, 1, 1)]
void ParticleEmitCS(uint3 id : SV_DispatchThreadID)
{
	if (id.x < NumDeadParticles && id.x < MaxParticlesThisFrame)
	{
		GPUParticlePartA pa = (GPUParticlePartA)0;
		GPUParticlePartB pb = (GPUParticlePartB)0;

		float2 uv = float2(id.x / 1024.0, ElapsedTime);
		float3 randomValues0 = RandomTexture.SampleLevel(LinearWrapSampler, uv, 0).xyz;

        float2 uv2 = float2((id.x + 1) / 1024.0, ElapsedTime);
        float3 randomValues1 = RandomTexture.SampleLevel(LinearWrapSampler, uv2, 0).xyz;

        pa.TintAndAlpha = float4(1, 1, 1, 1);
        pa.Rotation = 0;
        pa.IsSleeping = 0;
		
		float velocityMagnitude = length(EmitterVelocity.xyz);
        pb.Position = EmitterPosition.xyz + (randomValues0.xyz * PositionVariance.xyz);
		pb.Mass = Mass;
        pb.Velocity = EmitterVelocity.xyz + (randomValues1.xyz * velocityMagnitude * VelocityVariance);
		pb.Lifespan = ParticleLifeSpan;
		pb.Age = pb.Lifespan;
		pb.StartSize = StartSize;
		pb.EndSize = EndSize;

		uint index = DeadListToAllocFrom.Consume();
		ParticleBufferA[index] = pa;
		ParticleBufferB[index] = pb;
	}
}